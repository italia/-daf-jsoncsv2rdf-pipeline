PREFIX jarql:   <http://jarql.com/>
PREFIX rdfs:    <http://www.w3.org/2000/01/rdf-schema#>
PREFIX schema:  <http://schema.org/>
PREFIX clvapit: <http://dati.gov.it/onto/clvapit#>

CONSTRUCT {
    ?uri rdfs:label ?label ;
         schema:description ?description ;
         clvapit:hasCity ?uriComune ;
         clvapit:hasAddress ?indirizzo;
         clvapit:hasProvince ?labelProvincia;
         schema:telephone ?telefono;
         schema:faxNumber ?fax .

    ?uriComune rdfs:label ?labelComune .

}
WHERE {
    OPTIONAL { ?s jarql:NOME ?label }
    OPTIONAL { ?s jarql:COMUNE ?labelComune }
    OPTIONAL { ?s jarql:PROVINCIA ?labelProvincia }
    OPTIONAL { ?s jarql:CAP ?cap }
    OPTIONAL { ?s jarql:LOCALITA ?indirizzoLocalita }
    OPTIONAL { ?s jarql:INDIRIZZO ?indirizzoNomeViaPiazza }
    OPTIONAL { ?s jarql:NUMERO_CIVICO ?indirizzoNumeroCivico }
    OPTIONAL { ?s jarql:EMAIL ?email }
    OPTIONAL { ?s jarql:NUMERO_TELEFONO ?telefono }
    OPTIONAL { ?s jarql:NUMERO_FAX ?fax }
    OPTIONAL { ?s jarql:ATTIVE_PASSIVE ?statoAttivita }

    BIND(URI(CONCAT("http://dati.gov.it/dataset/biblioteche-umbria/", sha1(CONCAT(?label, " - ", ?labelComune)))) AS ?uri)
    BIND(CONCAT(?label, " - ", ?labelComune) AS ?description)
    BIND(URI(CONCAT("http://dati.gov.it/dataset/biblioteche-umbria/comuni/", ?labelComune)) AS ?uriComune)
    BIND(CONCAT(?indirizzoNomeViaPiazza, ", ", ?indirizzoNumeroCivico, " ", ?indirizzoLocalita, " ", ?cap, " - ", ?labelComune) AS ?indirizzo)
}
